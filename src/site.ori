// This file defines the overall structure of the WESL site
{
  // Static assets
  assets/
  
  // All the pages as objects with properties for title + body HTML. This
  // collection and the others below with names in parentheses are
  // non-enumerable, so will not be included in the final build output. The docs
  // and spec areas will end up with some unused pages that aren't exposed in
  // the navigation.
  (documents) = {
    index.html = index.ori.html/
    search.html = search.ori.html/
    docs/ = Tree.mapExtension(wiki, ".md->.html", { value: docPage.ori })
    spec/ = {
      ...Tree.mapExtension(wesl-spec, ".md->.html", { value: specPage.ori })
      // Assets for spec pages so assets links still work
      assets/ = wesl-spec/assets
    }
  }

  // Apply base page template to all bodies
  (pages/) = Tree.deepMap(Tree.from(documents), (value, key) =>
    Origami.extension.match(key, ".html") ? page.ori.html(value) : value
  )

  // Merge in all the pages
  ...pages

  // Create search indexes/resources the first time they're requested
  pagefind/ = Origami.once(() => package:@weborigami/pagefind(pages))
}
