// This file defines the overall structure of the WESL site
{
  // Static assets
  assets

  // All the page bodies, used three ways: for nav, for content, and for search.
  (documents) = {
    index.html = marked.js(index.md)
    docs/ = map(wiki.ori, { extension: ".md->.html", value: docPage.ori })
    spec/ = map(spec.ori, { extension: ".md->.html", value: docPage.ori })
  }

  // Left navigation links
  (nav): nav.ori(documents)

  // Apply base page template to all bodies and include nav
  // TODO [Jan]: `from` shouldn't be necessary
  (pages/) = deepMap(from(documents), (document) => page.ori.html(document, nav))

  // Merge in all the pages
  ...pages

  // Search page and indexes/resources created on demand (only once)
  search.html = page.ori.html(search.ori.html(), nav)
  pagefind/ = once(=package:@weborigami/pagefind(pages))
}
