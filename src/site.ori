// This file defines the overall structure of the WESL site
{
  // Static assets
  assets

  // All the page bodies, used three ways: for nav, for content, and for search.
  // This property and the other proprties with names in parentheses are
  // non-enumerable, so will not be included in the final build output.
  (documents) = {
    index.html = index.ori(wiki.ori/Home.md) // Repurpose wiki Home page
    search.html = search.ori.html()
    docs/ = map(wiki.ori, { extension: ".md->.html", value: docPage.ori })
    spec/ = map(spec.ori, { extension: ".md->.html", value: specPage.ori })
  }

  // HTML for the navigation links
  (nav): nav.ori(documents)

  // Apply base page template to all bodies and include navigation
  // TODO [Jan]: `from` shouldn't be necessary
  (pages/) = deepMap(from(documents), (document) => page.ori.html(document, nav))

  // Merge in all the pages
  ...pages

  // Create search indexes/resources the first time they're requested
  pagefind/ = once(=package:@weborigami/pagefind(pages))
}
